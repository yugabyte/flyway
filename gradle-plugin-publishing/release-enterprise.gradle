// First, apply the publishing plugin
buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
    classpath "org.apache.maven:maven-model:3.5.0"
  }
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

configurations {
    downloaded
}

dependencies {
    downloaded  "org.flywaydb.enterprise:flyway-gradle-plugin:" + System.getProperty("version")
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'maven'

configurations.archives.artifacts.clear()
artifacts {
    archives configurations.downloaded.resolve()[0]
}

version = System.getProperty("version")
group = "org.flywaydb.enterprise"

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'https://flywaydb.org/'
  vcsUrl = 'https://github.com/flyway/flyway'
  description = "Flyway by Redgate is an open-source database migration tool. It strongly favors simplicity and convention over configuration. It is simple, focused and powerful. It runs on Windows, Mac OSX and Linux, Java and Android. It is based around just 6 basic commands: Migrate, Clean, Info, Validate, Baseline and Repair. Migrations can be written in SQL (database-specific syntax (such as PL/SQL, T-SQL, ...) is supported) or Java (for advanced data transformations or dealing with LOBs). It has a Command-line client. If you are on the JVM, we recommend using the Java API (also works on Android) for migrating the database on application startup. Alternatively, you can also use the Maven plugin, Gradle plugin, SBT plugin or the Ant tasks. And if that not enough, there are plugins available for Spring Boot, Dropwizard, Grails, Play, Griffon, Grunt, Ninja and more! Supported databases are Oracle, SQL Server, SQL Azure, DB2, DB2 z/OS, MySQL (including Amazon RDS), MariaDB, Google Cloud SQL, PostgreSQL (including Amazon RDS and Heroku), Redshift, Vertica, H2, Hsql Derby, SQLite and solidDB. More info: https://flywaydb.org"
  tags = ['deployment', 'continuousdelivery', 'continuousdeployment', 'flyway', 'database migrations', 'schema evolution', 'devops']

  plugins {
    flywayPlugin {
      id = 'org.flywaydb.enterprise.flyway'
      displayName = 'Flyway Enterprise Gradle plugin'
    }
  }
  
  withDependencies { dependencies ->
    dependencies.add(new org.apache.maven.model.Dependency(groupId: 'org.flywaydb.enterprise', artifactId: 'flyway-core', version: System.getProperty("version")))
  }
}
